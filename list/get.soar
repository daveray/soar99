##########################################################################
# Operator list*get - get the kth value of a list
#
# ^operator
#   ^name list*get
#   ^head <head-of-list>
#   ^index desired index
#   ^return-to <name-of-result-attribute>

sp {list*get*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*get <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result)
  (<req> ^head nil
         ^index nil)
}

sp {list*get*propose*advance
  (state <s> ^name list*get
             ^head <head>
             ^index <index>
            -^result)
-->
  (<s> ^operator <o>)
  (<o> ^name list*advance
       ^count <index>
       ^head <head>)
}

sp {list*get*propose*return
  (state <s> ^name list*get
             ^result)
-->
  (<s> ^operator.name list*get-return)
}

sp {list*get*elaborate*return-nil
  (state <s> ^name list*get
             ^result.head nil)
-->
  (<s> ^return nil)
}

sp {list*get*elaborate*return-non-nil
  (state <s> ^name list*get
             ^result.head.value <value>)
-->
  (<s> ^return <value>)
}

sp {list*get*apply*return
  (state <s> ^name list*get
             ^operator.name list*get-return
             ^superstate <ss>
             ^return <value> 
             ^return-to <return-to>)
-->
  (<ss> ^<return-to> <value>)
}

