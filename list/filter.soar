##!
# Construct a list by filtering values accepted by an operator:
#
# ^operator
#   ^name list*filter
#   ^head <head-of-list>
#   ^func <name-of-operator-to-apply>
#   ^return-to <result-attribute>
#
# The filter operator to apply will be proposed like this:
#
# ^operator
#   ^name  <name-of-operator-to-apply>
#   ^value <list-value-to-process>
#   ^index <current-list-index>

sp {list*filter*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*filter <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result)
  (<req> ^head nil
         ^func nil)
}

sp {list*filter*propose*init
  (state <s> ^name list*filter
            -^current)
-->
  (<s> ^operator.name list*filter-init)
}

sp {list*filter*apply*init
  (state <s> ^name list*filter
             ^operator.name list*filter-init
             ^head <head>)
-->
  (<s> ^current <head>
       ^result-head nil
       ^index 0)
}

sp {list*filter*propose*func
  "If we're not at the end of the list, propose the func operator on 
   the current node's value. The operator will be applied..."
  (state <s> ^name list*filter
             ^func <func>
            -^result
             ^current.value <value>
             ^index <index>)
-->
  (<s> ^operator <o>)
  (<o> ^name <func>
       ^value <value>
       ^index <index>)
}

sp {list*filter*propose*process-func-result
  "If the func operator has posted a result, propose processing the
   result"
  (state <s> ^name list*filter
             ^result)
-->
  (<s> ^operator.name list*filter*process-func-result)
}


sp {list*filter*apply*process-func-result*first
  "Process the function's first result where we set up the head of
   the new list we're constructing"
  (state <s> ^name list*filter
             ^operator.name list*filter*process-func-result
             ^result true
             ^result-head <result-head>
             ^current.value <value>
            -^result-end)
-->
  (<s> ^result-head <result-head> - <new-result>
       ^result-end <new-result>)
  (<new-result> ^value <value> 
                ^next nil)
}

sp {list*filter*apply*process-func-result
  "Process subsequent function results after the head of the new list
   has been started"
  (state <s> ^name list*filter
             ^operator.name list*filter*process-func-result
             ^result true
             ^current.value <value>
             ^result-end <result-end>)
-->
  (<s> ^result-end <result-end> - <new-result>)
  (<result-end> ^next nil - <new-result>)
  (<new-result> ^value <value> 
                ^next nil)
}

sp {list*filter*apply*process-func-result*advance
  "Advance the current pointer in the list while processing 
   most recent function result"
  (state <s> ^name list*filter
             ^operator.name list*filter*process-func-result
             ^result <result>
             ^current <current>
             ^index <index>)
  (<current> ^next <next>)
-->
  (<s> ^result <result> -
       ^current <current> - <next>
       ^index <index> - (+ <index> 1))
}

sp {list*filter*propose*return
  "If we've reached the end of the list, propose the return operator"
  (state <s> ^name list*filter
             ^current nil)
-->
  (<s> ^operator.name list*filter-return)
}

sp {list*filter*apply*process-return
  "Return the resulting list"
  (state <s> ^name list*filter
             ^operator.name list*filter-return
             ^superstate <ss>
             ^result-head <result-head>
             ^return-to <return-to>)
-->
  (<ss> ^<return-to> <result-head>)
}

