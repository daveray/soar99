##########################################################################
# Operator list*zip - zip two lists together
#
# ^operator
#   ^name list*zip
#   ^first <head-of-first-list>
#   ^second <head-of-second-list>
#   ^default <default-value-when-list-runs-out> (defaults to nil)
#   ^return-to <name-of-result-attribute> (defaults to "result")
#
# The structure of the values of the result list looks like:
#
# ^value
#    ^first  <value-from-first-list>
#    ^second <value-from-second-list>

sp {list*zip*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*zip <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result
         ^default nil)
  (<req> ^first nil
         ^second nil)
}

sp {list*zip*propose*init
  (state <s> ^name list*zip 
            -^cfirst 
            -^csecond)
-->
  (<s> ^operator.name list*zip-init)
}

sp {list*zip*apply*init
  (state <s> ^name list*zip
             ^operator.name list*zip-init
             ^first <first>
             ^second <second>)
-->
  (<s> ^cfirst <first>
       ^csecond <second>
       ^builder <builder>)
  (<builder> ^head nil
             ^end nil)
}

sp {list*zip*propose*return
  (state <s> ^name list*zip 
             ^cfirst nil
             ^csecond nil)
-->
  (<s> ^operator.name list*zip-return)
}

###########################################################################

sp {list*zip*propose*prepare-value-both
  (state <s> ^name list*zip 
            -^value
             ^cfirst.value  <first-value>
             ^csecond.value <second-value>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*zip*prepare-value
       ^first <first-value>
       ^second <second-value>)
}

sp {list*zip*propose*prepare-value-first-done
  (state <s> ^name list*zip 
            -^value
             ^cfirst nil
             ^csecond.value <second-value>
             ^default <default>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*zip*prepare-value
       ^first <default>
       ^second <second-value>)
}

sp {list*zip*propose*prepare-value-second-done
  (state <s> ^name list*zip 
            -^value
             ^cfirst.value <first-value>
             ^csecond nil
             ^default <default>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*zip*prepare-value
       ^first <first-value>
       ^second <default>)
}

sp {list*zip*apply*prepare-value
  (state <s> ^name list*zip
             ^operator <o>)
  (<o> ^name list*zip*prepare-value
       ^first <first>
       ^second <second>)
-->
  (<s> ^value <value>)
  (<value> ^first <first>
           ^second <second>)
}

############################################################################

sp {list*zip*propose*continue-both
  (state <s> ^name list*zip 
             ^value <value>
             ^cfirst.next  <first-next>
             ^csecond.next <second-next>
             ^builder <builder>)
  (<builder> ^end)
-->
  (<s> ^operator <o>)
  (<o> ^name list*builder*add
       ^this <builder>
       ^value <value>
       ^first-next <first-next>
       ^second-next <second-next>)
}

sp {list*zip*propose*continue-first-done
  (state <s> ^name list*zip 
             ^value <value>
             ^cfirst nil
             ^csecond.next <second-next>
             ^builder <builder>)
  (<builder> ^end)
-->
  (<s> ^operator <o>)
  (<o> ^name list*builder*add
       ^this <builder>
       ^value <value>
       ^first-next nil
       ^second-next <second-next>)
}

sp {list*zip*propose*continue-second-done
  (state <s> ^name list*zip 
             ^value <value>
             ^cfirst.next <first-next>
             ^csecond nil
             ^builder <builder>
             ^default <default>)
  (<builder> ^end)
-->
  (<s> ^operator <o>)
  (<o> ^name list*builder*add
       ^this <builder>
       ^value <value>
       ^first-next <first-next>
       ^second-next nil)
}

sp {list*zip*apply*continue-advance
  (state <s> ^name list*zip
             ^value <value>
             ^operator <o>
             ^cfirst <cfirst>
             ^csecond <csecond>)
  (<o> ^name list*builder*add
       ^first-next <fn>
       ^second-next <sn>)
-->
  (<s> ^value <value> -
       ^cfirst <cfirst> - <fn>
       ^csecond <csecond> - <sn>)
}

sp {list*zip*apply*return
  (state <s> ^name list*zip 
             ^superstate <ss>
             ^return-to <return-to>
             ^operator.name list*zip-return
             ^builder.head <result>)
-->
  (<ss> ^<return-to> <result>)
}

