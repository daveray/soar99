##########################################################################
# Operators list*append-fast - Append one list onto another without modifying
# either list.
#
# Operators list*append-fast - Destructively append one list onto the end of 
# another.
#
# Both operators have the same interface:
#
# ^operator
#   ^name << list*append list*append-fast >>
#   ^first <head-of-first-list>
#   ^second <head-of-second-list>
#   ^return-to <name-of-result-attribute> (defaults to "result")
#
# This file has functionality common between append and append-fast

sp {list*append*elaborate*info
  "Add info about this operator to the lib*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*append <args>
         ^list*append-fast <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^first nil
         ^second nil
         ^return-to result)
}

sp {list*append*propose*return-nil
  "If both lists are empty, just return nil"
  (state <s> ^name << list*append list*append-fast >>
             ^first nil
             ^second nil)
-->
  (<s> ^operator <o>)
  (<o> ^name list*append*return
       ^result nil)
}

sp {list*append*propose*return-first
  "If the second list is empty, just return the first"
  (state <s> ^name << list*append list*append-fast >>
             ^first {<first> <> nil}
             ^second nil)
-->
  (<s> ^operator <o>)
  (<o> ^name list*append*return
       ^result <first>)
}

sp {list*append*propose*return-second
  "If the first list is empty, just return the second"
  (state <s> ^name << list*append list*append-fast >>
             ^first nil
             ^second {<second> <> nil})
-->
  (<s> ^operator <o>)
  (<o> ^name list*append*return
       ^result <second>)
}

sp {list*append*apply*return
  (state <s> ^name << list*append list*append-fast >>
             ^operator <o>
             ^return-to <return-to>
             ^superstate <ss>)
  (<o> ^name list*append*return
       ^result <result>)
-->
  (<ss> ^<return-to> <result>)
}
