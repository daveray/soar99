# See append-common.soar

sp {list*append*propose*copy-first
  "Copy the first list by applying identity map"
  (state <s> ^name list*append 
             ^first {<first> <> nil}
             ^second <> nil
            -^copy-of-first)
-->
  (<s> ^operator <o>)
  (<o> ^name list*map
       ^func list*identity
       ^head <first>
       ^return-to copy-of-first)
}

sp {list*append*propose*append-fast
  "Now that the first list is copied, delegate to append-fast to destructively
   append the second to the copy of the first"
  (state <s> ^name list*append
             ^copy-of-first <first>
             ^second <second>
            -^result <result>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*append-fast
       ^first <first>
       ^second <second>)
}

sp {list*append*propose*return
  "When append-fast has completed, return the new list."
  (state <s> ^name list*append
             ^result <result>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*append*return
       ^result <result>)
}

