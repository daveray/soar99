##########################################################################
# Operator list*slice - take a slice of a list based on an integer range
#
# ^operator
#   ^name list*slice
#   ^head <head-of-list>
#   ^start <start-index>, defaults to 0
#   ^end   <end-index>, defaults to end of list
#   ^return-to <name-of-result-attribute>
#
# This implementation uses list*filter to just filter by index rather than
# reimplementing all of the list traversal stuff again.

sp {list*slice*elaborate*info
  "Add info about this operator to the list*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*slice <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result
         ^start 0
         ^end   2147483647)
  (<req> ^head nil)
}

sp {list*slice*apply*filter*func-true
  "If the current index is inside of [start, end) return true"
  (state <s> ^superstate <ss> 
             ^operator <o>)
  (<ss> ^name list*slice
        ^start <start>
        ^end   <end>)
  (<o> ^name list*slice-filter 
       ^index { <index> >= <start> < <end> })
-->
  (<s> ^result true)
}

sp {list*slice*apply*filter*func-false
  "If the current index is outside of [start, end) return false"
  (state <s> ^superstate <ss> 
             ^operator <o>)
  (<ss> ^name list*slice
        ^start <start>
        ^end   <end>)
  (<o> ^name list*slice-filter)
 -{ # OR
   -{ (<o> ^index { <index> < <start> }) }
   -{ (<o> ^index { <index> >= <end> }) }
  }
-->
  (<s> ^result false)
}

sp {list*slice*propose*filter
  "If list*slice is proposed and there's no result, propose list*filter
   to filter the list based on index"
  (state <s> ^name list*slice
            -^result
             ^head <head>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*filter
       ^head <head>
       ^func list*slice-filter)
}

sp {list*slice*propose*return
  (state <s> ^name list*slice
             ^result)
-->
  (<s> ^operator.name list*slice-return)
}

sp {list*slice*apply*return
  (state <s> ^name list*slice
             ^operator.name list*slice-return
             ^result <result>
             ^superstate <ss>
             ^return-to <return-to>)
-->
  (<ss> ^<return-to> <result>)
}
