##!
#
# ^operator
#   ^name list*range
#   ^start <start-value>
#   ^end   <end-value>
#   ^step  <step-value>

sp {list*range*elaborate*info
  "Add info about this operator to the list*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*range <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result
          ^start 0
          ^step 1)
  (<req> ^end nil)
}

sp {list*range*propose*init
  (state <s> ^name list*range
            -^value)
-->
  (<s> ^operator.name list*range-init)
}

sp {list*range*apply*init
  "Set up the initial state for the operator, including the initial builder
   for constructing the result list"
  (state <s> ^name list*range
             ^operator.name list*range-init
             ^start <start>
             ^end <end>
             ^step <step>)
-->
  (<s> ^value <start>
       ^builder <builder>)
  (<builder> ^head nil
             ^end nil)
}

sp {list*range*propose*advance
  "If we're not past the end of the range, propose adding another 
   value to the result list"
  (state <s> ^name list*range
             ^end <end>
             ^value { <value> <= <end> }
             ^builder <builder>)
-->
  (<s> ^operator <o>)
  (<o> ^name list*builder*add
       ^this <builder>
       ^value <value>)
}

sp {list*range*apply*advance
  "While adding to the result list, calculate next value using current value
   and step"
  (state <s> ^name list*range
             ^operator.name list*builder*add
             ^value <old-value>
             ^step <step>)
-->
  (<s> ^value <old-value> - (+ <old-value> <step>))
}

sp {list*range*propose*return
  "If we're at the end of the range, propose return"
  (state <s> ^name list*range
             ^end <end>
             ^value > <end>)
-->
  (<s> ^operator.name list*range-return)
}

sp {list*range*apply*return
  "Put result list on the parent state"
  (state <s> ^name list*range
             ^operator.name list*range-return
             ^superstate <ss>
             ^return-to <return-to>
             ^builder.head <result-head>)
-->
  (<ss> ^<return-to> <result-head>)
}

