##!
#
# ^operator
#   ^name list*range
#   ^start <start-value>
#   ^end   <end-value>
#   ^step  <step-value>

sp {list*range*elaborate*info
  "Add info about this operator to the list*library structure on the
   top state."
  (state <s> ^superstate nil
             ^list*library.operators <ops>)
-->
  (<ops> ^list*range <args>)
  (<args> ^default <def> 
          ^required <req>)
  (<def> ^return-to result
          ^start 0
          ^step 1)
  (<req> ^end nil)
}

sp {list*range*propose*init
  (state <s> ^name list*range
            -^value)
-->
  (<s> ^operator.name list*range-init)
}

sp {list*range*apply*init
  (state <s> ^name list*range
             ^operator.name list*range-init
             ^start <start>
             ^end <end>
             ^step <step>)
-->
  (<s> ^value <start>
       ^result-head nil)
}

sp {list*range*propose*advance
  (state <s> ^name list*range
             ^end <end>
             ^value { <value> <= <end> })
-->
  (<s> ^operator.name list*range-advance)
}

sp {list*range*apply*advance
  (state <s> ^name list*range
             ^operator.name list*range-advance
             ^value <value>
             ^step <step>
             ^result-head <result-head>)
-->
  (<s> ^value <value> - (+ <value> <step>)
       ^result-head <result-head> - <new-node>)
  (<new-node> ^value <value>
              ^next <result-head>)
}

sp {list*range*propose*reverse
  (state <s> ^name list*range
             ^result-head <result-head>
             ^end <end>
             ^value > <end>
            -^reversed-result)
-->
  (<s> ^operator <o>)
  (<o> ^name list*reverse
       ^head <result-head>
       ^return-to reversed-result)
}


sp {list*range*propose*return
  (state <s> ^name list*range
             ^reversed-result)
-->
  (<s> ^operator.name list*range-return)
}

sp {list*range*apply*return
  (state <s> ^name list*range
             ^operator.name list*range-return
             ^superstate <ss>
             ^return-to <return-to>
             ^reversed-result <result-head>)
-->
  (<ss> ^<return-to> <result-head>)
}

